' Siemens PD1165 displays

' this rev uses simpler I/F with 74374 

' ==========================
' pins
' ==========================

' 8-bit Address & Data Bus
inbus		var	INL
outbus		var	OUTL
busdir		var	DIRL

' Address write line (active low)
_wr_addr	var	OUT8
_wr_addr_dir	var	DIR8
_wr_addr_pin	con	8

' Data write line (active low)
_wr_data	var	OUT9
_wr_data_dir	var	DIR9
_wr_data_pin	con	9


' ==========================
' data
' ==========================

' array holding display bits
dispmem		data	0 (72)		' corresponds to display columns


' ==========================
' general workspace
' ==========================
i		var	BYTE
j		var	BYTE
w		var	WORD
x		var	WORD



' =================================
' parameters for write_data routine
' =================================
column		var	byte		' display column 0-71
rowdata		var	byte		' 8-bits of row data




' ==========================
' init
' ==========================

' bus defaults to output
busdir = %11111111

' write lines are outputs
_wr_addr_dir = 1
_wr_data_dir = 1


' write lines must start HIGH
_wr_addr = 1
_wr_data = 1




' ==========================
' Initialize the PD1165
' ==========================

' initialize the master display
outbus = %00001000
pulsout _wr_addr_pin,1

' set data as appropriate
outbus = %00000111
pulsout _wr_data_pin,1

' initialize 8 slave displays
for i = 1 to 8
	outbus = %00001000 | (i << 4)
	pulsout _wr_addr_pin,1

	outbus = %00000111
	pulsout _wr_data_pin,1
next


w = 0
loop:

	random w
	rowdata = w.lowbyte

	for j = 1 to 10
		for i = 0 to 24
			column = i
			gosub write_data
		x = rowdata
		rowdata = (x << 1) | x.bit7
		next
	next
goto loop





' ==========================
' WRITE routine
' ==========================

' Parameters:
'
'	column	- column to write to 0-71
'	rowdata	- 8-bit row data to write

write_data:

	' convert column index to actual address
	'
	' 	low 3 bits specify 1 of 8 columns in a single display
	'	high 4 bits specify 1 of 9 display units
	'	(unused bit is for control word in displays)

	outbus = ((column / 8) << 4) + ((column // 8) & %111)

	pulsout _wr_addr_pin,1

	outbus = rowdata
	pulsout _wr_data_pin,1

	' remember this bit pattern
'	write dispmem+column, rowdata

return
